buildscript {
    repositories {
        maven { url = "https://maven.aliyun.com/repository/public" }
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
    id 'org.jetbrains.dokka' version '1.8.10'
    id 'maven-publish'
    id 'signing'
}

group = 'top.kmar.game'
archivesBaseName = 'cg-engine'
version = '1.0.2'

repositories {
    maven { url = "https://maven.aliyun.com/repository/public" }
    mavenCentral()
}

dependencies {
    implementation group: 'it.unimi.dsi', name: 'fastutil-core', version: '8.5.12'
}

kotlin {
    jvmToolchain(8)
}

compileKotlin {
    compilerOptions {
        freeCompilerArgs.addAll(
                "-Xuse-k2",                     // k2
                "-Xjvm-default=all",            // 接口 default 方法
                "-Xlambdas=indy",               // indy
                "-Xassertions=always-disable",  // 禁用断言
                "-Xsam-conversions=indy",
                "-Xvalidate-bytecode"           // 优化
        )
    }
}

java {
    withSourcesJar()
}

dokkaHtml {
    outputDirectory.set(file("build/documentation/html"))
}

tasks.register('dokkaJavadocJar', Jar.class) {
    dependsOn(dokkaHtml)
    from(dokkaHtml)
    archiveClassifier.set("javadoc")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(tasks["dokkaJavadocJar"])

            artifactId archivesBaseName

            pom {
                name = 'Console Game Engine'
                description = 'A console game engine for the JVM.'
                url = 'https://github.com/EmptyDreams/ConsoleGameEngine'

                scm {
                    url = 'https://github.com/EmptyDreams/ConsoleGameEngine'
                    connection = 'scm:git:git:github.com/EmptyDreams/ConsoleGameEngine.git'
                    developerConnection = 'scm:git:ssh://git@github.com:EmptyDreams/ConsoleGameEngine.git'
                }

                licenses {
                    license {
                        name = 'The Affero General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id = 'Kmar'
                        name = '空梦'
                        email = 'minedreams@qq.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = archivesBaseName

            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}